#include "stddef.gdh"

table(glyph)

	//	Classes

	cls_matraToBase = ( g_aamatra g_ematra g_eematra g_aimatra
		g_omatra g_oomatra g_aumatra );
	cls_matras = ( g_imatra g_iimatra g_umatra g_uumatra
		g_rvocalicmatra g_rrvocalicmatra cls_matraToBase );
	cls_takesLargeEmatra = ( g_nga g_nya g_rra );
	cls_takesUbasedMatra = ( g_pa g_pa_base g_pha g_pha_base g_va g_va_base );
	cls_UbasedMatra = ( g_umatra g_uumatra g_omatra g_oomatra );
	cls_LargeUbasedMatra = ( g_umatra_large g_uumatra_large
		g_omatra_large g_oomatra_large );
	cls_takesLargeRaSubCons = ( g_gha g_cha g_jha g_ddha
		g_tha g_dha g_pha g_bha );
	cls_cons = ( cno_sub g_ra );
	cls_subCons = ( csub g_ra_below );
	cls_vowel = ( g_avowel g_aavowel g_ivowel g_iivowel g_uvowel g_uuvowel
		g_rvocalicvowel g_lvocalicvowel g_evowel g_eevowel g_aivowel g_ovowel
		g_oovowel g_auvowel );

endtable; // glyph

table(substitution)

pass(1)

	// akhn
	g_ka g_virama g_ssa > _ g_ka g_ssa_sub:(1 2);
	g_ja g_virama g_nya > _ g_ja g_nya_sub:(1 2);

endpass;

pass(2)

	// rphf

	// advance past consonants in cluster
	g_shortrr _ > _ @1:(1 2) / _ g_virama cls_cons ^ _;

	// advance past matras
	g_shortrr _ > _ @1:(1 2) / _ cls_matras ^ _;

	// advance past sub forms
	g_shortrr _ > _ @1:(1 2) / _ cls_subCons ^ _;

	// block ra->reph for virama ra
	g_ra g_virama > @2 @3 / g_virama _ _ cls_cons;

	// initial ra->reph
	g_ra g_virama _ > _ _ g_shortrr:(1 2 3) / _ _ cls_cons ^ _;

endpass;

pass(3)

	// blwf

	// most consonants
	g_virama cls_cons > _ cls_subCons:(1 2) / cls_cons _ _;
	g_virama cls_cons > _ cls_subCons:(1 2) / cls_subCons _ _;

	// special handling of ra sub form
	g_shortrr > g_ra_below_ra / g_ra _;

endpass;

pass(4)

	// #define opt(x)      [x]?
	// #define opt2(x)     [opt(x) x]?
	// #define opt3(x)     [opt2(x) x]?

	// move matras over sub forms

	// one sub form
	cls_cons cls_subCons cls_matras > @1 @3 @2;
	cls_cons cls_subCons cls_matras cls_matras > @1 @3 @4 @2;
	// cls_cons cls_subCons cls_matras cls_matras cls_matras > @1 @3 @4 @5 @2;

	// two sub forms
	cls_cons cls_subCons cls_subCons cls_matras > @1 @4 @2 @3;
	cls_cons cls_subCons cls_subCons cls_matras cls_matras > @1 @4 @5 @2 @3;
	// cls_cons cls_subCons cls_subCons cls_matras cls_matras cls_matras > @1 @4 @5 @6 @2 @3;

endpass;

pass(5)

	// pres2? abvs

	// vowel signs AA, E, EE, AI, O, OO, AU
	cno_base > cbase / _ cls_matraToBase;

	// different E matra
	g_ematra > g_ematra_large / cls_takesLargeEmatra _;

	// vowel signs I, II
	cno_imathra g_imatra  > cimathra:2 _;
	cno_imathra g_iimatra > cimathra g_lengthmark;

endpass;

pass(6)

	// blws

	// conjuncts
	g_ta_sub g_ya_sub > _ g_ta_ya_sub:(1 2);
	g_ta_sub g_va_sub > _ g_ta_va_sub:(1 2);

	// special handling of ra sub forms
	g_ra_below > g_ra_below_angleright / cls_subCons _;
	g_ra_below > g_ra_below_large / cls_takesLargeRaSubCons _;

	// TODO:
	// Should
	// ka_rvocalicmatra.sub, ta_rvocalicmatra.sub,
	// ta_ailengthmark.sub, ra_ailengthmark.sub
	// be here as well?

endpass;

pass(7)

	// psts
	cls_UbasedMatra > cls_LargeUbasedMatra / cls_takesUbasedMatra _;

	// handle fully decomposed cases
	// here the uumatra is part of an omatra or oomatra in the input
	g_uumatra > g_uumatra_large / cls_takesUbasedMatra g_ematra _;

endpass;

pass(8)

	// haln
	cno_base > cbase / _ g_virama;

endpass;

endtable; // substitution

// POSITIONING

/* Most positioning done in autogenerated .gdl but
     specific cases can be added below */

// table(positioning)


// endtable; // positioning
